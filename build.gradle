plugins {
  id 'java-library'

  // To create a fat jar build/libs/options-all.jar, run: ./gradlew shadowJar
  id 'com.github.johnrengelman.shadow' version '7.1.2'

  // Formatting is disabled due to poor handling of type annotations; wait until
  // this issue is fixed: https://github.com/google/google-java-format/issues/5
  // // Code formatting with Google Java Format; defines targets "goJF" and "verJF".
  // id 'com.github.sherter.google-java-format' version '0.7.1'

  // Error Prone linter
  id("net.ltgt.errorprone") version "2.0.2"

  // Checker Framework pluggable type-checking
  id 'org.checkerframework' version '0.6.15'
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.plumelib:reflection-util:1.0.5'
  implementation 'org.apache.commons:commons-text:1.9'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
  implementation 'io.github.classgraph:classgraph:4.8.149'

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// To upload to Maven Central, see instructions in the file.
apply from: "${buildscript.sourceFile.parent}/gradle/mavencentral.gradle"

// Work around https://github.com/johnrengelman/shadow/issues/324
configurations {
  toolsJar  // tools.jar contains the Javadoc tool
}
dependencies {
  if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
    toolsJar files(org.gradle.internal.jvm.Jvm.current().toolsJar)
  }
}
configurations.compileClasspath.extendsFrom configurations.toolsJar

test {
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
        excludeEngines 'junit-vintage'
    }
}

/// Error Prone linter

dependencies {
  errorprone("com.google.errorprone:error_prone_core:2.15.0")
}
tasks.withType(JavaCompile).configureEach {
  // "-classfile" is because of classgraph and https://bugs.openjdk.java.net/browse/JDK-8190452
  options.compilerArgs << "-Xlint:all,-processing,-classfile" << "-Werror"
  options.errorprone {
    enabled = JavaVersion.current() != JavaVersion.VERSION_1_8
    disable("ReferenceEquality") // Use Interning Checker instead
    disable("StringSplitter") // obscure case isn't likely
    disable("AnnotateFormatMethod") // Error Prone doesn't know about Checker Framework @FormatMethod
    disable("UseCorrectAssertInTests") // https://github.com/typetools/checker-framework/issues/3345
  }
}

/// Checker Framework pluggable type-checking

apply plugin: 'org.checkerframework'

checkerFramework {
  checkers = [
    // No need to run CalledMethodsChecker, because ResourceLeakChecker does so.
    // 'org.checkerframework.checker.calledmethods.CalledMethodsChecker',
    'org.checkerframework.checker.formatter.FormatterChecker',
    'org.checkerframework.checker.index.IndexChecker',
    'org.checkerframework.checker.interning.InterningChecker',
    'org.checkerframework.checker.lock.LockChecker',
    'org.checkerframework.checker.nullness.NullnessChecker',
    'org.checkerframework.checker.regex.RegexChecker',
    'org.checkerframework.checker.resourceleak.ResourceLeakChecker',
    'org.checkerframework.checker.signature.SignatureChecker',
    'org.checkerframework.checker.signedness.SignednessChecker',
    'org.checkerframework.common.initializedfields.InitializedFieldsChecker',
  ]
  extraJavacArgs = [
    '-Werror',
    '-AcheckPurityAnnotations',
    '-ArequirePrefixInWarningSuppressions',
    '-AwarnUnneededSuppressions',
    // `com.sun.javadoc` is deprecated and superseded by `jdk.javadoc.doclet`.
    //  `jdk.javadoc.doclet` only exists in Java 9 and later, so switching to it means losing support for Java 8.
    // Classes are imported by OptionsDoclet and therefore this warning can't be suppressed there.
    '-Xlint:-removal',
  ]
}

// To use a locally-built Checker Framework, run gradle with "-PcfLocal".
if (project.hasProperty("cfLocal")) {
  def cfHome = String.valueOf(System.getenv("CHECKERFRAMEWORK"))
  dependencies {
    compileOnly files(cfHome + "/checker/dist/checker-qual.jar")
    testCompileOnly files(cfHome + "/checker/dist/checker-qual.jar")
    checkerFramework files(cfHome + "/checker/dist/checker.jar")
  }
}

/// Javadoc

// Turn Javadoc warnings into errors.
javadoc {
  options.addStringOption('Xwerror', '-Xdoclint:all')
  options.addStringOption('private', '-quiet')
  if (JavaVersion.current().compareTo(org.gradle.api.JavaVersion.VERSION_1_9) >= 0) {
    options.addStringOption('source', '8')
  }
  doLast {
    ant.replaceregexp(match:"@import url\\('resources/fonts/dejavu.css'\\);\\s*", replace:'',
      flags:'g', byline:true) {
      fileset(dir: destinationDir)
    }
  }
}
check.dependsOn javadoc

task javadocWeb(type: Javadoc) {
  description "Upload API documentation to website."
  source = sourceSets.main.allJava
  destinationDir = file("/cse/web/research/plumelib/${project.name}/api")
  classpath = project.sourceSets.main.compileClasspath
  if (JavaVersion.current().compareTo(org.gradle.api.JavaVersion.VERSION_1_9) >= 0) {
      options.addStringOption("-release", "8");
  }
  doLast {
    // Set permissions
    project.exec {
      commandLine('chgrp', '-R',  'plse_www', "/cse/web/research/plumelib/${project.name}/api")
    }
    project.exec {
      commandLine('chmod', '-R',  'g+w', "/cse/web/research/plumelib/${project.name}/api")
    }
  }
}

configurations {
  requireJavadoc
}
dependencies {
  requireJavadoc "org.plumelib:require-javadoc:1.0.4"
}
task requireJavadoc(type: JavaExec) {
  // Gradle resolves requireJavadoc's Option dependency to the jar built by this project,
  // so declare an explicit dependency on jar.
  dependsOn jar
  description = 'Ensures that Javadoc documentation exists.'
  mainClass = "org.plumelib.javadoc.RequireJavadoc"
  classpath = configurations.requireJavadoc
  args "src/main/java"
}
check.dependsOn requireJavadoc
javadocWeb.dependsOn requireJavadoc

/// Emacs support

/* Make Emacs TAGS table */
task tags(type: Exec) {
  description "Run etags to create an Emacs TAGS table"
  commandLine "bash", "-c", "find src/ -name '*.java' | sort | xargs etags"
}
