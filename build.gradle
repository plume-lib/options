plugins {
  // To create a fat jar build/libs/options-all.jar, run: ./gradlew shadowJar
  id "com.github.johnrengelman.shadow" version "2.0.4"

  // Run the Error Prone linter.
  id "net.ltgt.errorprone-base" version "0.0.16"
  // Checker Framework pluggable type-checking
  id 'org.checkerframework' version '0.3.4'

  // Formatting is disabled due to poor handling of type annotations; wait until
  // this issue is fixed: https://github.com/google/google-java-format/issues/5
  // // Code formatting with Google Java Format; defines targets "goJF" and "verJF".
  // id "com.github.sherter.google-java-format" version "0.7.1"
}

apply plugin: 'java-library'

// To upload to Maven Central, see instructions in the file.
apply from: "${buildscript.sourceFile.parent}/gradle/mavencentral.gradle"

dependencies {
  // https://mvnrepository.com/artifact/org.apache.commons/commons-text
  implementation 'org.apache.commons:commons-text:1.6'
  // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
  // https://mvnrepository.com/artifact/junit/junit
  testImplementation 'junit:junit:4.12'
  errorprone 'com.google.errorprone:error_prone_core:2.3.3'
}

repositories {
  mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Work around https://github.com/johnrengelman/shadow/issues/324
configurations {
  toolsJar  // tools.jar contains the Javadoc tool
}
dependencies {
  toolsJar files(org.gradle.internal.jvm.Jvm.current().toolsJar)
}
configurations.compileClasspath.extendsFrom configurations.toolsJar

// Checker Framework pluggable type-checking
apply plugin: 'org.checkerframework'
dependencies {
  compileOnly 'org.checkerframework:checker-qual:2.8.2'
  testCompileOnly 'org.checkerframework:checker-qual:2.8.2'
  checkerFramework 'org.checkerframework:checker:2.8.2'
  checkerFrameworkAnnotatedJDK 'org.checkerframework:jdk8:2.8.2'
}
checkerFramework {
  checkers = [
    'org.checkerframework.checker.formatter.FormatterChecker',
    'org.checkerframework.checker.index.IndexChecker',
    'org.checkerframework.checker.interning.InterningChecker',
    'org.checkerframework.checker.lock.LockChecker',
    'org.checkerframework.checker.nullness.NullnessChecker',
    'org.checkerframework.checker.nullness.NullnessRawnessChecker',
    'org.checkerframework.checker.regex.RegexChecker',
    'org.checkerframework.checker.signature.SignatureChecker'
  ]
  extraJavacArgs = [
    '-AcheckPurityAnnotations',
    "-Astubs=checker.jar/javadoc.astub"
  ]
}


/// Javadoc

// Turn Javadoc warnings into errors.
javadoc {
  options.addStringOption('Xwerror', '-Xdoclint:all')
  options.addStringOption('private', '-quiet')
}

task javadocWeb(type: Javadoc) {
  source = sourceSets.main.allJava
  destinationDir = file("/cse/web/research/plumelib/options/api")
  classpath = project.sourceSets.main.compileClasspath
}

dependencies {
  compileOnly group: 'org.plumelib', name: 'require-javadoc', version: '0.1.2'
}
task requireJavadoc(type: Javadoc) {
  description = 'Ensures that Javadoc documentation exists.'
  destinationDir.deleteDir()
  source = sourceSets.main.allJava
  classpath = project.sourceSets.main.compileClasspath
  // options.memberLevel = JavadocMemberLevel.PRIVATE
  options.docletpath = project.sourceSets.main.compileClasspath as List
  options.doclet = "org.plumelib.javadoc.RequireJavadoc"
}


/// Emacs support

/* Make Emacs TAGS table */
task tags(type: Exec) {
  description "Run etags to create an Emacs TAGS table"
  commandLine "bash", "-c", "find src/ -name '*.java' | sort | xargs etags"
}
